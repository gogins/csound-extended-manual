<html>
<head>
    <title>Minimal Example using Csound for WebAssembly</title>
 </head>
<body style="background-color:LightGrey;">
    <script src="CsoundAudioNode.js"></script>
    <script src="csound_loader.js"></script>
    <script>
    var csound_message_callback = function(message) {
        var messages_textarea = document.getElementById("console");
        var existing = messages_textarea.value;
        messages_textarea.value = existing + message;
        messages_textarea.scrollTop = messages_textarea.scrollHeight;
    }
    var onPlayClick = async function() {
        let messages_textarea = document.getElementById("console");
        messages_textarea.value = "";
        let csound_ = await get_csound(csound_message_callback);
        if (csound_ == null) {
            return;
        }
        let seconds = await csound_.GetScoreTime();
        csound_message_callback('Score time: ' + seconds + '\n');
        if (seconds > 0) {
            csound_message_callback('Already playing...\n');
            return;
        }
        let csd = document.getElementById('csd').value;
        await csound_.CompileCsdText(csd);
        await csound_.Start();
        await csound_.Perform();
        
    }
    var onPlayStop = async function() {
        let csound_ = await get_csound(csound_message_callback);
        let seconds = await csound_.GetScoreTime();
        csound_message_callback("Score time: " + seconds + "\n");
        if (seconds > 0) {
            await csound_.Stop();
            await csound_.Cleanup();
            await csound_.Reset();
    }
        
    }
  </script>
<h1 style="font-family:sans-serif;">gen14</h1>
<p>
This should play if your Web browser has WebAssembly enabled (most do). Most examples will play unless they need to load files. The first time you click <i>Play</i>, Csound will spend a few seconds loading, then play. You can edit this code and replay it. 
</p>
<p>
<input type="button" value="Play" onclick="onPlayClick()"/>
<input type="button" value="Stop" onclick="onPlayStop()"/>
<p>
<textarea id="csd" style="width:98vw;height:45vh;font-family:monospace;background-color:#050570;color:#F0F090;"><CsoundSynthesizer>
<CsOptions>
; Select audio/midi flags here according to platform
-odac  ;;;realtime audio out
;-iadc    ;;;uncomment -iadc if realtime audio input is needed too
; For Non-realtime ouput leave only the line below:
; -o gen14.wav -W ;;; for file output any platform
</CsOptions>
<CsInstruments>

; Required settings for WebAudio:

sr = 48000
ksmps = 128
nchnls = 2
nchnls_i = 1

; sr = 44100 
; ksmps = 32 
; nchnls = 2 
0dbfs  = 1 
;after the example from The Csound Book, page 83

instr 1	;compare results from GEN13 & GEN14
		
iwshpfn	= p6	
inrmfn	= p7	
aswp	linseg	0.01, p3*.5, .49, p3*.5, 0.01		;index sweep function
aindex	poscil	aswp, p5, 2				;sound to waveshape
atable	tablei	aindex, iwshpfn, 1, .5			;waveshape index
anrm	tablei	aswp*2, inrmfn, 1			;normalization 
aenv	linen	p4, .01, p3, .02			;amplitude envelope
asig	= (atable*anrm)*aenv				;normalize and impose envelope
asig    dcblock2 asig					;get rid of DC
	outs    asig, asig

endin		
</CsInstruments>
<CsScore>

f 2 0 8192 10 1			;sine wave

f 28  0   4097 13  1 1 1 0 .8 0 .5 0 .2		;waveshaping function: GEN13 - odd harmonics
f 280 0   2049 4   28 1				;normalization function for f28
f 29  0   4097 14  1 1 1 0 .8 0 .5 0 .2		;waveshaping function: GEN14 - same harmonics
f 290 0   2049 4   29 1				;normalization function for f29


f 30  0   4097 13  1 1 0 1 0 .6 0 .4 0 .1	;waveshaping function: GEN13 - even harmonics
f 301 0   2049 4   30 1				;normalization function for f30
f 31  0   4097 14  1 1 0 1 0 .6 0 .4 0 .1	;waveshaping function: GEN13 - even harmonics
f 310 0   2049 4   31 1				;normalization function for 31
s
i1 0   3   .7   440 28  280 
i1 4   .   .7    .  29  290
i1 8   .   .7    .  30  301
i1 12  3   .7    .  31  310

e
</CsScore>
</CsoundSynthesizer>

</textarea>
<textarea id="console" readonly style="width:98vw;height:40vh;font-family:monospace;background-color:DarkSlateGrey;color:LawnGreen;">
</textarea>
</body>
</html>
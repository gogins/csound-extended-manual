<html>
<head>
    <title>Minimal Example using Csound for WebAssembly</title>
 </head>
<body style="background-color:LightGrey;">
    <script src="CsoundAudioNode.js"></script>
    <script src="csound_loader.js"></script>
    <script>
    var csound_message_callback = function(message) {
        var messages_textarea = document.getElementById("console");
        var existing = messages_textarea.value;
        messages_textarea.value = existing + message;
        messages_textarea.scrollTop = messages_textarea.scrollHeight;
    }
    var onPlayClick = async function() {
        let messages_textarea = document.getElementById("console");
        messages_textarea.value = "";
        let csound_ = await get_csound(csound_message_callback);
        if (csound_ == null) {
            return;
        }
        let csd = document.getElementById('csd').value;
        await csound_.Stop();
        await csound_.Cleanup();
        await csound_.Reset();
        await csound_.CompileCsdText(csd);
        await csound_.Start();
        await csound_.Perform();
        
    }
    var onPlayStop = async function() {
        let csound_ = await get_csound(csound_message_callback);
        await csound_.Stop();
        await csound_.Cleanup();
        await csound_.Reset();
    }
  </script>
<h1 style="font-family:sans-serif;">gen17</h1>
<p>
This should play if your Web browser has WebAssembly enabled (most do). Most examples will play unless they need to load files. The first time you click <i>Play</i>, Csound will spend a few seconds loading, then play. You can edit this code and replay it. 
</p>
<p>
<input type="button" value="Play" onclick="onPlayClick()"/>
<input type="button" value="Stop" onclick="onPlayStop()"/>
<input type="button" value="Back" onclick="window.history.back()"/>
<p>
<textarea id="csd" style="width:98vw;height:45vh;font-family:monospace;background-color:#050570;color:#F0F090;"><CsoundSynthesizer>
<CsOptions>
; Select audio/midi flags here according to platform
-odac   ;;;realtime audio out
;-iadc    ;;;uncomment -iadc if realtime audio input is needed too
; For Non-realtime ouput leave only the line below:
; -o gen17.wav -W ;;; for file output any platform
; By Stefano Cucchi & Menno Knevel - 2021
</CsOptions>
<CsInstruments>

; Required settings for WebAudio:

sr = 48000
ksmps = 128
nchnls = 2
nchnls_i = 1

; sr     =    44100
; ksmps  =    32
; nchnls =    2
0dbfs =     1

instr 1 
gisqre ftgen 2, 0, 16384, 10, 1, 0 , .33, 0, .2 , 0, .14, 0 , .11, 0, .09 ;odd harmonics

knoteleft oscil 1, 0.5, 10       ; index to table 10 - gen 17 - every 2 seconds reads all the values in the table n. 10
printks2 "note left  = %d\n", knoteleft
knoteright oscil 1, 1, 10        ; index to table 10 - gen 17 - every  second reads all the values in the table n. 10
printks2 "note right = %d\n", knoteright

ixmode =    1
ixoff =     0
iwrap =     1
aphasor1 phasor knoteleft        ; the values in table 10 become the frequency of the oscillator
asig1 tablei aphasor1, gisqre, ixmode, ixoff, iwrap ; oscillator generating sound in the left channel (table 10 every 2 seconds)
aphasor2 phasor knoteright       ; the values in table 10 become the frequency of the oscillator
asig2 tablei aphasor2, gisqre, ixmode, ixoff, iwrap ; oscillator generating sound in the right channel (table 10 every second)
kgenenv linseg 0, 0.3, 0.4, p3-0.6, 0.4, 0.3, 0     ; envelope
         outs asig1 * kgenenv, asig2 * kgenenv
endin

</CsInstruments>
<CsScore>
; table with gen17 - At point 0 pitch 300 Hz, at point 12 pitch 350 Hz, etc...
f  10  0  128  -17   0  300   12  350   20  400   41  434   48  563  67  589   72  632   79  678  100 712 120 789

i1 0 10 
e
</CsScore>
</CsoundSynthesizer>
</textarea>
<textarea id="console" readonly style="width:98vw;height:40vh;font-family:monospace;background-color:DarkSlateGrey;color:LawnGreen;">
</textarea>
</body>
</html>
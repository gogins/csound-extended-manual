<html>
<head>
    <title>Minimal Example using Csound for WebAssembly</title>
 </head>
<body style="background-color:LightGrey;">
    <script src="CsoundAudioNode.js"></script>
    <script src="csound_loader.js"></script>
    <script>
    var csound_message_callback = function(message) {
        var messages_textarea = document.getElementById("console");
        var existing = messages_textarea.value;
        messages_textarea.value = existing + message;
        messages_textarea.scrollTop = messages_textarea.scrollHeight;
    }
    var onPlayClick = async function() {
        let messages_textarea = document.getElementById("console");
        messages_textarea.value = "";
        let csound_ = await get_csound(csound_message_callback);
        if (csound_ == null) {
            return;
        }
        let csd = document.getElementById('csd').value;
        await csound_.Stop();
        await csound_.Cleanup();
        await csound_.Reset();
        await csound_.CompileCsdText(csd);
        await csound_.Start();
        await csound_.Perform();
        
    }
    var onPlayStop = async function() {
        let csound_ = await get_csound(csound_message_callback);
        await csound_.Stop();
        await csound_.Cleanup();
        await csound_.Reset();
    }
  </script>
<h1 style="font-family:sans-serif;">pvsinfo</h1>
<p>
This should play if your Web browser has WebAssembly enabled (most do). Most examples will play unless they need to load files. The first time you click <i>Play</i>, Csound will spend a few seconds loading, then play. You can edit this code and replay it. 
</p>
<p>
<input type="button" value="Play" onclick="onPlayClick()"/>
<input type="button" value="Stop" onclick="onPlayStop()"/>
<input type="button" value="Back" onclick="window.history.back()"/>
<p>
<textarea id="csd" style="width:98vw;height:45vh;font-family:monospace;background-color:#050570;color:#F0F090;"><CsoundSynthesizer>
<CsOptions>
; Select audio/midi flags here according to platform
-odac  ;;;realtime audio out
;-iadc    ;;;uncomment -iadc if realtime audio input is needed too
; For Non-realtime ouput leave only the line below:
; -o pvsinfo.wav -W ;;; for file output any platform
</CsOptions>
<CsInstruments>

; Required settings for WebAudio:

sr = 48000
ksmps = 128
nchnls = 2
nchnls_i = 1

; sr = 44100
; ksmps = 32
; nchnls = 2
0dbfs  = 1

; by Menno Knevel 2021

; analyze sound file and output result to pvoc-ex file
ires  system_i 1,{{ pvanal fox.wav fox.pvx }}          ; default settings
ires1 system_i 1,{{ pvanal -n256 MSjungleMid.wav MSjungleMid.pvx }}    ; different frame size

instr 1

if p4 == 0 then
    idur filelen  "fox.pvx"		;find duration of analysis file
    kpos line     0,p3,idur		;to ensure we process whole file
    fsrc pvsfread kpos, "fox.pvx"	;create fsig from (mono) file
    prints  "\n---***you now hear the analyzed file fox.pvx***---"
else
    idur filelen  "MSjungleMid.pvx"		;find duration of analysis file
    kpos line     0,p3,idur		;to ensure we process whole file
    fsrc pvsfread kpos, "MSjungleMid.pvx"	;create fsig from (mono) file
    prints  "\n---***you now hear the analyzed file MSjungleMid.pvx***---"
endif

iovl,inb,iws,ifmt pvsinfo fsrc	;get info
prints "\n          Overlap size   = %d\n", iovl  ;print info
prints "          Number of bins = %d\n",inb
prints "          Window size    = %d\n",iws
prints "          Frame Format   = %d\n",ifmt
prints "\n"
aout pvsynth fsrc
     outs aout, aout

endin
</CsInstruments>
<CsScore>
i 1 0  3     0  ; the fox
i 1 5 13     1  ; the jungle
e
</CsScore>
</CsoundSynthesizer>
</textarea>
<textarea id="console" readonly style="width:98vw;height:40vh;font-family:monospace;background-color:DarkSlateGrey;color:LawnGreen;">
</textarea>
</body>
</html>
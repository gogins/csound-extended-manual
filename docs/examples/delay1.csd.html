<html>
<head>
    <title>Minimal Example using Csound for WebAssembly</title>
 </head>
<body style="background-color:LightGrey;">
    <script src="CsoundAudioNode.js"></script>
    <script src="csound_loader.js"></script>
    <script>
    var csound_message_callback = function(message) {
        var messages_textarea = document.getElementById("console");
        var existing = messages_textarea.value;
        messages_textarea.value = existing + message;
        messages_textarea.scrollTop = messages_textarea.scrollHeight;
    }
    var onPlayClick = async function() {
        let messages_textarea = document.getElementById("console");
        messages_textarea.value = "";
        let csound_ = await get_csound(csound_message_callback);
        if (csound_ == null) {
            return;
        }
        let csd = document.getElementById('csd').value;
        await csound_.Stop();
        await csound_.Cleanup();
        await csound_.Reset();
        await csound_.CompileCsdText(csd);
        await csound_.Start();
        await csound_.Perform();
        
    }
    var onPlayStop = async function() {
        let csound_ = await get_csound(csound_message_callback);
        await csound_.Stop();
        await csound_.Cleanup();
        await csound_.Reset();
    }
  </script>
<h1 style="font-family:sans-serif;">delay1</h1>
<p>
This should play if your Web browser has WebAssembly enabled (most do). Most examples will play unless they need to load files. The first time you click <i>Play</i>, Csound will spend a few seconds loading, then play. You can edit this code and replay it. 
</p>
<p>
<input type="button" value="Play" onclick="onPlayClick()"/>
<input type="button" value="Stop" onclick="onPlayStop()"/>
<input type="button" value="Back" onclick="window.history.back()"/>
<p>
<textarea id="csd" style="width:98vw;height:45vh;font-family:monospace;background-color:#050570;color:#F0F090;"><CsoundSynthesizer>
<CsOptions>
; Select audio/midi flags here according to platform
; Audio out   Audio in
-odac             ;;;RT audio out
; For Non-realtime ouput leave only the line below:
; -o delay1.wav -W ;;; for file output any platform
</CsOptions>
<CsInstruments>

; Required settings for WebAudio:

sr = 48000
ksmps = 128
nchnls = 2
nchnls_i = 1

; by Menno Knevel 2021, after Russel Pinkston

; sr = 44100
; ksmps = 32
; nchnls = 2
0dbfs  = 1


instr 1                             ; original sounds

ichoice =   p4
if ichoice == 0 then
        prints  "\n--**original beats**--\n\n"
    aout    diskin2 "beats.wav"
else
        prints  "\n--**original noise**--\n\n"
    aout    rand .2
endif
outs    aout, aout

endin

instr 2                             ; Finite Impulse Response (FIR) Filter
                                    
ichoice =   p4
if ichoice == 0 then
    prints  "\n--**FIRST-ORDER LOW-PASS on the beats**--\n\n"
    aout    diskin2 "beats.wav"
else
    prints  "\n--**FIRST-ORDER LOW-PASS on the noise**--\n\n"
    aout    rand .2
endif

adel1   delay1  aout                ; delay 1 sample        			
asig    =   (.5* aout)+(.5* adel1)	; average 2 succesive inputs
outs    asig, asig

endin


instr 3                             ; Finite Impulse Response (FIR) Filter        
                                    
ichoice =   p4
if ichoice == 0 then
    prints  "\n--**FIRST-ORDER HIGH-PASS on the beats**--\n\n"
    aout    diskin2 "beats.wav"
else
    prints  "\n--**FIRST-ORDER HIGH-PASS on the noise**--\n\n"
    aout    rand .2
endif

adel1   delay1  aout                ; delay 1 sample
asig    =   (.5*aout)-(.5*adel1)   	; difference of 2 inputs
outs    asig, asig

endin

instr 4                             ; Finite Impulse Response (FIR) Filter        
                                    
ichoice =   p4
if ichoice == 0 then
    prints  "\n--**SECOND-ORDER NOTCH on the beats**--\n\n"
    aout    diskin2 "beats.wav"
else
    prints  "\n--**SECOND-ORDER NOTCH on the noise**--\n\n"
    aout    rand .2
endif

adel1   delay1  aout                ; x(n - 1)
adel2   delay1  adel1               ; x(n - 2)
asig   	=   (.5*aout)+(.5*adel2)  	; y(n) = .5x(n) + .5x(n - 2)
outs    asig, asig

endin

instr 5                             ; Finite Impulse Response (FIR) Filter        
                                    
ichoice =   p4
if ichoice == 0 then
    prints  "\n--**SECOND-ORDER BAND-PASS on the beats**--\n\n"
    aout    diskin2 "beats.wav"
else
    prints  "\n--**SECOND-ORDER BAND-PASS on the noise**--\n\n"
    aout    rand .2
endif

adel1   delay1  aout                ; x(n - 1)
adel2   delay1  adel1               ; x(n - 2)
asig   	=   (.5*aout)-(.5*adel2)  	; y(n) = .5x(n) - .5x(n - 2)
outs    asig, asig

endin

</CsInstruments>
<CsScore>

i1   0     2    0   
i1   3     2    1 
s
i2   1     2    0 
i2   4     2    1   
s
i3   1     2    0 
i3   4     2    1 
s
i4   1     2    0 
i4   4     2    1 
s
i5   1     2    0 
i5   4     2    1 
e
</CsScore>
</CsoundSynthesizer>
</textarea>
<textarea id="console" readonly style="width:98vw;height:40vh;font-family:monospace;background-color:DarkSlateGrey;color:LawnGreen;">
</textarea>
</body>
</html>
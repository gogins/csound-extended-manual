<html>
<head>
    <title>Minimal Example using Csound for WebAssembly</title>
 </head>
<body style="background-color:LightGrey;">
    <script src="CsoundAudioNode.js"></script>
    <script src="csound_loader.js"></script>
    <script>
    var csound_message_callback = function(message) {
        var messages_textarea = document.getElementById("console");
        var existing = messages_textarea.value;
        messages_textarea.value = existing + message;
        messages_textarea.scrollTop = messages_textarea.scrollHeight;
    }
    var onPlayClick = async function() {
        let messages_textarea = document.getElementById("console");
        messages_textarea.value = "";
        let csound_ = await get_csound(csound_message_callback);
        if (csound_ == null) {
            return;
        }
        let seconds = await csound_.GetScoreTime();
        csound_message_callback('Score time: ' + seconds + '\n');
        if (seconds > 0) {
            csound_message_callback('Already playing...\n');
            return;
        }
        let csd = document.getElementById('csd').value;
        await csound_.CompileCsdText(csd);
        await csound_.Start();
        await csound_.Perform();
        
    }
    var onPlayStop = async function() {
        let csound_ = await get_csound(csound_message_callback);
        let seconds = await csound_.GetScoreTime();
        csound_message_callback("Score time: " + seconds + "\n");
        if (seconds > 0) {
            await csound_.Stop();
            await csound_.Cleanup();
            await csound_.Reset();
    }
        
    }
  </script>
<h1 style="font-family:sans-serif;">scanu-musical</h1>
<p>
This should play if your Web browser has WebAssembly enabled (most do). Most examples will play unless they need to load files. The first time you click <i>Play</i>, Csound will spend a few seconds loading, then play. You can edit this code and replay it. 
</p>
<p>
<input type="button" value="Play" onclick="onPlayClick()"/>
<input type="button" value="Stop" onclick="onPlayStop()"/>
<p>
<textarea id="csd" style="width:98vw;height:45vh;font-family:monospace;background-color:#050570;color:#F0F090;"><CsoundSynthesizer>
<CsOptions>
-odac -d -F "MIDIpad.mid"
; For Non-realtime ouput leave only the line below:
; -o scanu-musical.wav -W ;;; for file output any platform
</CsOptions>
<CsInstruments>

; Required settings for WebAudio:

sr = 48000
ksmps = 128
nchnls = 2
nchnls_i = 1

; by Shengzheng Zhang

; sr = 44100
; ksmps = 32
; nchnls = 2
0dbfs  = 1

itmp  ftgen 1, 0, 128, 7, 0, 64, 1,64, 0      ; generating all bunch of tables needed
itmp  ftgen 2, 0, 128, -7, 1, 128, 1
itmp  ftgen 3, 0, 16384, -23, "string-128.matrxB"
itmp  ftgen 4, 0, 128,-7 ,0.5 ,128, 0
itmp  ftgen 5, 0, 128, -7, 1, 128, 1
itmp  ftgen 6, 0, 128, -7, 0, 128, 0
itmp  ftgen 7, 0, 128, -7, .001, 128,128

gal   init 0                                 ; getting reverb and delay ready
gar   init 0
gavl  init 0
gavr  init 0

instr 1
kpan    randh 1, 3                           ; random panning
kpan    port kpan, 0.05 ;make the panning change not so quick
kpitch  cpsmidib 12
ipitch  cpsmidi
kenvm   madsr 0.02, 0.6, 0.2, 1
kenv2   madsr 0.1, 0.6, 0.8, 1
kenv3   madsr 0.8, 0.6, 0.8, 1
kvib    lfo 1, 5
ifnvel  = 1
ifnmass = 2
ifnstif = 3
ifncentr = 4
ifndamp = 5
kstif   = 0.3 + 0.1*kvib * kenv3
kmass   = 10-(5*kenvm)                      ; modulated mass
kcentr  = 0.1
kdamp   = -0.02
ileft   = 0.2
iright  = 0.8
kpos    = 0.2
kstrngth = 0.005
;setups for scan synthesis
a2      madsr 0.002, 0.01, 0.001, 0 ;get a initial pick, but seemed like not working 
scanu 1, 0.1,ifnvel, ifnmass,ifnstif,ifncentr, ifndamp,kmass, kstif, kcentr, kdamp, ileft, iright,kpos,kstrngth, a2*0.5, 0.2, 1
a1      scans .01*kenv2, kpitch+kpitch*kvib*kenv3*0.01, 7, 1
a1      dcblock a1
a1      butterlp	a1, kpitch*8
a1      butterlp	a1, kpitch*12
al      = a1*kpan
ar      = a1*(1-kpan)
        outs al, ar
        vincr gal, al
        vincr gar, ar
        vincr gavl, al
        vincr gavr, ar
endin

instr 2
abuf    delayr 5
adl     deltap 0.3
        delayw gal + (adl * 0.2)

abuf    delayr 5
adr     deltap 0.4
        delayw gal + (adr * 0.2)
        
        outs adl, adr
        clear gal
        clear gar
endin

instr 3
adl, adr reverbsc gavl, gavr, 0.85, 12000
        outs adl, adr
        clear gavl
        clear gavr
endin

</CsInstruments>
<CsScore>
i2 0 70
i3 0 70
e
</CsScore>
</CsoundSynthesizer>
</textarea>
<textarea id="console" readonly style="width:98vw;height:40vh;font-family:monospace;background-color:DarkSlateGrey;color:LawnGreen;">
</textarea>
</body>
</html>
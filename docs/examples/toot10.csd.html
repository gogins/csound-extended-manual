<html>
<head>
    <title>Minimal Example using Csound for WebAssembly</title>
 </head>
<body style="background-color:LightGrey;">
    <script src="CsoundAudioNode.js"></script>
    <script src="csound_loader.js"></script>
    <script>
    var csound_message_callback = function(message) {
        var messages_textarea = document.getElementById("console");
        var existing = messages_textarea.value;
        messages_textarea.value = existing + message;
        messages_textarea.scrollTop = messages_textarea.scrollHeight;
    }
    var onPlayClick = async function() {
        let messages_textarea = document.getElementById("console");
        messages_textarea.value = "";
        let csound_ = await get_csound(csound_message_callback);
        if (csound_ == null) {
            return;
        }
        if (csound_.IsPlaying() == true) {
            csound_message_callback('Already playing...\n');
            return;
        }
        let csd = document.getElementById('csd').value;
        await csound_.CompileCsdText(csd);
        await csound_.Start();
        await csound_.Perform();
        
    }
    var onPlayStop = async function() {
        let csound_ = await get_csound(csound_message_callback);
        await csound_.Stop();
        await csound_.Cleanup();
        await csound_.Reset();
    }
  </script>
<h1 style="font-family:sans-serif;">toot10</h1>
<p>
This should play if your Web browser has WebAssembly enabled (most do). Most examples will play unless they need to load files. The first time you click <i>Play</i>, Csound will spend a few seconds loading, then play. You can edit this code and replay it. 
</p>
<p>
<input type="button" value="Play" onclick="onPlayClick()"/>
<input type="button" value="Stop" onclick="onPlayStop()"/>
<input type="button" value="Back" onclick="window.history.back()"/>
<p>
<textarea id="csd" style="width:98vw;height:45vh;font-family:monospace;background-color:#050570;color:#F0F090;"><CsoundSynthesizer>
<CsOptions>
-odac
</CsOptions>
<CsInstruments>

; Required settings for WebAudio:

sr = 48000
ksmps = 128
nchnls = 2
nchnls_i = 1

; nchnls    =         2

garvbsig init       0


          instr 10

iattack   =         .01
irelease  =         .2
iwhite    =         22050
idur      =         p3
iamp      =         p4
iswpstart =         p5
isweepend =         p6
ibndwidth =         p7
ibalance  =         p8                  ; 1 = left, .5 = center, 0 = right
irvbgain  =         p9

kamp      linen     iamp, iattack, idur, irelease
ksweep    line      iswpstart, idur, isweepend
asig      rand      iwhite
afilt     reson     asig, ksweep, ibndwidth
arampsig  =         kamp * afilt

          outs      arampsig * ibalance, arampsig * (1 - ibalance)

garvbsig  =         garvbsig + arampsig * p9

          endin

          instr     100

irvbtime  =         p4
asig      reverb    garvbsig,  irvbtime
          outs      asig, asig
garvbsig  =         0

          endin


</CsInstruments>
<CsScore>

;ins strt dur  rvbtime
i100 0    15   1.1
i100 15   10   5

;ins strt dur  amp   stsw   ndsw   bdw  bal(0-1)  rvsnd
i10  0    2    .01   5000   500    20   .15        .1
i10  3    1    .01   1500   5000   30   .95        .1
i10  5    2    .01   850    1100   40   .45        .1
i10  8    2    .01   1100   8000   50   .05        .1
i10  8    .5   .01   5000   1000   30   .35        .2
i10  9    .5   .01   1000   8000   40   .75        .1
i10  11   .5   .01   500    2100   50   .14        .2
i10  12   .5   .01   2100   1220   75   .96        .1
i10  13   .5   .01   1700   3500   100  .45        .2
i10  15   5    .005  8000   800    60   .85        .1

e
</CsScore>
</CsoundSynthesizer>
</textarea>
<textarea id="console" readonly style="width:98vw;height:40vh;font-family:monospace;background-color:DarkSlateGrey;color:LawnGreen;">
</textarea>
</body>
</html>